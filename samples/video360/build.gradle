buildscript {
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.0.1'
    }
}

repositories {
	google()
	jcenter()
}

apply plugin: 'com.android.application'

// Loading `local.properties` files for configuring
Properties localProperties = new Properties()
File localPropertiesFile = project.rootProject.file("local.properties")
if (localPropertiesFile.exists()) {
    localProperties.load(localPropertiesFile.newDataInputStream())
}

android {
    compileSdkVersion 26
    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 26

        // Here you we define credentials for using the VrPlayerLib
        // We recommend placing them in `local.properties` file
        buildConfigField "String", "SPHEREPLAY_CUSTOMER_ID", "\"" + localProperties.get("sphereplayCustomerId", "") + "\""
        buildConfigField "String", "SPHEREPLAY_LICENSE_KEY", "\"" + localProperties.get("sphereplayLicenseKey", "") + "\""
    }
}

dependencies {
    implementation 'com.sphereplay.vrplayerlib:sdk:2.0.0'
}
